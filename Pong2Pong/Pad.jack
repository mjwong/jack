class Pad {

   field int x, y; // screen location of the Pad's top-left corner
   field int size; // length of this Pad, in pixels

   /** Constructs a new Pad with a given location and size. */
   constructor Pad new(int Ax, int Ay, int Asize) {
      let x = Ax;
      let y = Ay;
      let size = Asize;
      do draw();
      return this;
   }

   /** Disposes this Pad. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /** Draws the Pad on the screen. */
   method void draw() {
      do Screen.setColor(true);
      do Screen.drawRectangle(x, y, x + 4, y + size);
      return;
   }

   /** Erases the Pad from the screen. */
   method void erase() {
      do Screen.setColor(false);
      do Screen.drawRectangle(x, y, x + 4, y + size);
      return;
   }
   /** Moves the Pad up by 1 pixels. */
   method void moveUp() {
      if (y > 1) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, (y + size) - 1, x + 4, y + size);
         let y = y - 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + 4, y + 1);
      }
      return;
   }

   /** Moves the Pad down by 1 pixels. */
   method void moveDown() {
      if ((y + size) < 254) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + 4, y + 1);
         let y = y + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, (y + size) - 1, x + 4, y + size);
      }
      return;
   }
   method int returnY(){
      return y;
   }
}
